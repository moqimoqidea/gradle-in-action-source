apply from: 'databaseSetup.gradle'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':model')
    runtimeOnly 'com.h2database:h2:1.3.170'
    testImplementation 'junit:junit:4.11'
}

test {
    reports.html.destination = file("$reports.html.destination/unit")
    reports.junitXml.destination = file("$reports.junitXml.destination/unit")
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        // moqiFIXME: 2021/7/31 00:31: 等待调试此错误依赖问题
        //  Could not determine the dependencies of task ':repository:integrationTest'.
        //  Resolving dependency configuration 'testRuntimeOnly' is not allowed as it is defined as 'canBeResolved=false'.
        //  Instead, a resolvable ('canBeResolved=true') dependency configuration that extends 'testRuntimeOnly' should be resolved.
        compileClasspath = sourceSets.main.output + configurations.testRuntimeOnly
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    dependsOn startAndPrepareDatabase
    finalizedBy stopDatabase
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

check.dependsOn integrationTest
