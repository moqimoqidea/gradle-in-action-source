configurations {
    cargo {
        description = 'Classpath for Cargo Ant tasks.'
        visible = false
    }
}

task deployToLocalTomcat {
    doLast {
        FileTree cargoDependencies = configurations.getByName('cargo').asFileTree
        ant.taskdef(resource: 'cargo.tasks', classpath: cargoDependencies.asPath)

        ant.cargo(containerId: 'tomcat7x', action: 'run', output: "$buildDir/output.log") {
            configuration {
                deployable(type: 'war', file: 'todo.war')
            }

            zipUrlInstaller(installUrl: 'http://archive.apache.org/dist/tomcat/tomcat-7/v7.0.32/bin/apache-tomcat-7.0.32.zip')
        }
    }
}

dependencies {
    cargo 'org.codehaus.cargo:cargo-ant:1.3.1'
}

repositories {
    mavenCentral()
}

// gradle -q dependencyInsight --configuration cargo --dependency xml-apis:xml-apis
// 观察版本被选择的原因: Selection reasons
configurations.cargo.resolutionStrategy {
    // 当遇到版本冲突时让构建失败
    failOnVersionConflict()
}
